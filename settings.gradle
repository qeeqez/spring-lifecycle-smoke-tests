pluginManagement {
	includeBuild "gradle/plugins/cr-smoke-test-plugin"
	repositories {
		gradlePluginPortal()
		maven {
			url "https://repo.spring.io/snapshot"
		}
		maven {
			url 'https://repo.spring.io/milestone'
		}
		maven {
			url "https://repo.spring.io/release"
			content {
				includeGroup "io.spring.ge.conventions"
			}
		}
	}
}

plugins {
	id "com.gradle.enterprise" version "3.16.2"
	id "io.spring.ge.conventions" version "0.0.15"
}

rootProject.name="spring-lifecycle-smoke-tests"

include "cr-smoke-test-support"
include "cr-listener"

["boot", "cloud", "data", "framework", "integration", "security", "session"].each { group ->
	file(group).eachDirMatch(~/[a-z].*/) { smokeTest ->
		include "$group:${smokeTest.name}"
	}
}
